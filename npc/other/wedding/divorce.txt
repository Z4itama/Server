//===== eAthena Script ======================================= 
//= Divorcing Deviruchi
//===== By: ================================================== 
//= Perkka, Scriptor, LightFighter
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= eAthena 1 +
//===== Description: ========================================= 
//=
//===== Additional Comments: ================================= 
//= Both players should be online to get divorced
//= Fixed menu [Lupus]
//= 1.1 Added check if the patner's online, added effects [Lupus]
//============================================================ 


nif_in.gat,190,112,5	script	Deviruchi#divorce	1109,{
	mes "[Deviruchi]";
	if (getpartnerid() == 0) {
		mes "Kekeke, humans are always alone in the end,";
		mes "The feeling of lonely is just temporarily.";
		mes "If you feel lonely, how about getting a pet?";
		mes "I, the Deviruchi is quite popular recently, you know...";
		close;
	}
	mes "You don't look good...";
	mes "Anything that you have done that makes you feel regret now?";
	mes "You must know that I am not willing to help happy people.";
	mes "Deviruchi bear no interest";
	mes "to those kind of people. Kekeke";
	next;
	mes "[Deviruchi]";
	mes "Let's put everything aside first.";
	mes "Let me ask you. Are you happy after you get married?";
	mes "Don't think about love...";
	mes "I know more than you can imagine.";
	mes "There're countless problem behind one grand wedding.";
	next;
	mes "[Deviruchi]";
	mes "That's right.";
	mes "It your fate that you come here.";
	mes "And I know it's your instinct to change your life too.";
	mes "Although you have made an oath to your partner,";
	mes "the heart will still wither somehow...!";
	next;
	mes "[Deviruchi]";
	mes "The method is easy!";
	mes "Marriage is actually one kind of contract.";
	mes "Just ask a favor from Deviruchi";
	mes "and it will destroy the contract for you... Do you get it?";
	mes "Live a free life!!";
	next;
	mes "[Deviruchi]";
	mes "You don't need to get the agreement of your partner,";
	mes "You will find another lover soon,";
	mes "So, why not leave your current partner?";
	mes "... Want me to help?";
	mes "I can cancel your marriage now, you know...!";
	next;
	menu "No, I am very happy now",-,"Yes, I want a new life",M_NewLife;

	mes "[Deviruchi]";
	mes "Hmmph, lovers... lovers!!";
	mes "You will regret,";
	mes "and come back to find Deviruchi someday!";
	mes "We'll see how long your happiness will last!!";
	close2;
	warp "niflheim.gat",169,162;
	end;

M_NewLife:
	mes "[Deviruchi]";
	mes "Good, you don't need anything.";
	mes "I just need your heart and 2,500,000 zeny.";
	mes "You want to get back your life before you get married, don't you?";
	mes "So, is that it? You're going to cancel the marriage contract?";
	next;
	menu "...I need to consider it again",-, "I am paying you to cancel it now!",M_Divorce;
	mes "[Deviruchi]";
	mes "Quit considering. Everyone will be alone in the end.";
	mes "Just make up your mind and prepare the zeny,";
	mes "then come and look for me...";
	mes "Kekekeke.....";
	close;

M_Divorce:
	mes "[Deviruchi]";
	if (!isloggedin(getpartnerid())) {
		mes "Kekeke, humans are so stupid,";
		mes "Your lover should be online... Kekeke";
		emotion e_heh;
		close;
	}
	if (Zeny < 2500000){
		mes "Haven't I said 2,500,000 zeny?";
		mes "This is the payment for Deviruchi to help you.";
		mes "If you don't even have that money,";
		mes "you can rot with that married life of yours... kekeke";
		close;
	}
	set Zeny, Zeny - 2500000;
	mes "Before you change your mind again, I will proceed with your divorce now!!";
	mes "It might take some time...";
	mes "So don't go away, and stay here!";
	next;
	specialeffect 244;	//EF_MAGICROD
	specialeffect2 259;	//EF_DEVIL (now use a custom id)
	divorce;
	percentheal -100,-100;
	mes "[Deviruchi]";
	mes "Kekeke, you are free now!";
	mes "Throw away all your sinking feelings!";
	mes "You should thank Deviruchi.....!";
	close;
}

// *****************************************************
//                 Item Security System
// *****************************************************

-	script	SecuritySystem	-1,{
	end;

OnSettings:
	while( 1 )
	{
		mes "[^FFA500Security System^000000]";
		mes "Current Status";

		mes "- Security Code:";
		if( #SECURITYCODE )
		{
			mes "^FFFFFF__________^2E8B57Enable^000000.";

			mes "- Item transfers:";
			if( getsecurity() )
			{
				mes "^FFFFFF__________^2E8B57Blocked^000000.";
				set .@Menu2$, "Allow Item transfers";
			}
			else
			{
				mes "^FFFFFF__________^FF0000Allowed^000000.";
				set .@Menu2$, "Block Item transfers";
			}
			set .@m$, "Change Security Password:"+ .@Menu2$ +":Remove Security Password:Exit";
			set .@offset, 0;
		}
		else
		{
			mes "^FFFFFF__________^FF0000Disable^000000.";
			set .@m$, "Set Password:Exit";
			set .@offset, 4;
		}
		next;
		
		set .@menu$, "Change Account Password:Change Account Email:"+ .@m$;
		set .@option, select(.@menu$);
		if(.@option > 2)
			set .@option, .@option + .@offset;
		
		switch(.@option)
		{
			case 1:
				if(#SECURITYCODE) {
				mes "[^FFA500Security System^000000]";
				mes "Enter your current security password";
				input .@Secpass;
				if( .@Secpass != #SECURITYCODE )
					{
					mes "[^FFA500Security System^000000]";
					mes "Wrong security password!!.";
					close;
					}					
					mes "Enter your current account password...";
					next;
					input .@password$;
					query_sql("SELECT user_pass FROM login WHERE account_id = "+ getcharid(3), .@userpass$);
					if( md5(.@password$) != .@userpass$ )
					{
						mes "[^FFA500Security System^000000]";
						mes "Wrong password!!.";
						close;
					}
					mes "[^FFA500Security System^000000]";
					mes "Max length of 24";
					mes "Enter your new password...";
					next;
					input .@password$;
					if( .@password$ == "" || getstrlen(.@password) > 24)
					{
						mes "[^FFA500Security System^000000]";
						mes "Invalid value!!.";
						close;
					}
					mes "[^FFA500Security System^000000]";
					mes "Confirm your new password.";
					next;
					input .@confirm$;
					if( .@confirm$ != .@password$ )
					{
						mes "[^FFA500Security System^000000]";
						mes "The passwords are different.";
						mes "Operation canceled.";
						close;
					}
					query_sql("UPDATE login SET user_pass = '"+ md5(.@password$) +"' WHERE account_id = "+ getcharid(3));
				}
				else {
					mes "[^FFA500Security System^000000]";
					mes "You can only change your account password when your security code setting is enabled.";
					next;
				}
				break;
			case 2:
				if(#SECURITYCODE) {
					mes "[^FFA500Security System^000000]";
					mes "Enter your current security password";
					input .@Secpass;
					if( .@Secpass != #SECURITYCODE )
					{
					mes "[^FFA500Security System^000000]";
					mes "Wrong security password!!.";
					close;
					}
					mes "Enter your new email...";
					next;
					input .@email$;
					set .@mail$, escape_sql(.@email$);
					if( .@email$ == "" || getstrlen(.@mail$) > 39 ) 
					{
						mes "[^FFA500Security System^000000]";
						mes "Invalid value!!.";
						close;
					}
					mes "[^FFA500Security System^000000]";
					mes "Confirm your new email.";
					next;
					input .@confirm$;
					if( .@confirm$ != .@email$ )
					{
						mes "[^FFA500Security System^000000]";
						mes "The emails are different.";
						mes "Operation canceled.";
						close;
					}
					query_sql("UPDATE login SET email = '"+ .@mail$ +"' WHERE account_id = "+ getcharid(3));
				}
				else {
					mes "[^FFA500Security System^000000]";
					mes "You can only change your account email when your security code setting is enabled.";
					next;
				}
				break;
			case 3: // Cambiar Clave
				mes "[^FFA500Security System^000000]";
				mes "Password is a number between 1000 and 999999.";
				mes "Enter your current password...";
				next;

				input .@Pass;
				if( .@Pass != #SECURITYCODE )
				{
					mes "[^FFA500Security System^000000]";
					mes "Wrong password!!.";
					close;
				}
			case 7:
				mes "[^FFA500Security System^000000]";
				mes "Enter your new password...";
				mes "Password is a number between 1000 and 999999.";
				next;
				
				input .@Pass;
				if( .@Pass < 1000 || .@Pass > 999999 )
				{
					mes "[^FFA500Security System^000000]";
					mes "Invalid value!!.";
					close;
				}
				
				mes "[^FFA500Security System^000000]";
				mes "Confirm your new password.";
				next;

				input .@CPass;
				if( .@CPass != .@Pass )
				{
					mes "[^FFA500Security System^000000]";
					mes "The passwords are different.";
					mes "Operation canceled.";
					close;
				}
				
				set #SECURITYCODE, .@Pass;
				break;
			case 4: // Permitir Salidas - Bloquear Salidas
				if( getsecurity() )
				{
					mes "[^FFA500Security System^000000]";
					mes "Please enter your Password to allow item transfers.";
					next;
					
					input .@Pass;
					if( #SECURITYCODE != .@Pass )
					{
						mes "[^FFA500Security System^000000]";
						mes "Wrong password!!.";
						close;
					}
					
					setsecurity 0;
				}
				else if( #SECURITYCODE > 0 )
					setsecurity 1;
				else
					setsecurity 0;
				break;
			case 5: // Quitar Clave
				mes "[^FFA500Security System^000000]";
				mes "Enter your current password.";
				next;
				
				input .@Pass;
				if( #SECURITYCODE != .@Pass )
				{
					mes "[^FFA500Security System^000000]";
					mes "Wrong password!!.";
					close;
				}
				
				set #SECURITYCODE, 0;
				setsecurity 0;
				break;
			case 6:
			case 8:
				mes "[^FFA500Security System^000000]";
				mes "Use @security if you need to configure your services again.";
				mes "Good Day...";
				close;
		}
	}
	
	end;
}